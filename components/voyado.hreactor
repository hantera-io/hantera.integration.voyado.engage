let baseUrl |> registry->'externalServices/voyado/baseUrl'
let apiKey |> registry->'externalServices/voyado/apiKey'

let findContact(email: text) =>
  web.http(
    $'{baseUrl}/api/v3/contacts/id?email={email}'
    {
      headers = {
        'User-Agent' -> 'Hantera'
        'apikey' -> $'{apiKey}'
        'Content-Type' -> 'application/json'
      }
    }
   ) match
    (res: HttpResponseOk) |> res.content

let getOrder(orderId: uuid) =>
  // Get current order state
  let orderQuery =
    query orders(orderState, currencyCode, invoiceAddress)
    filter $'orderId == {orderId}'
    navigate
      deliveries(deliveryId, deliveryNumber, deliveryState)
      navigate orderLines(productNumber, description, orderLineTotal, quantity, image)
    navigate
      paymentCaptures(amount)
      navigate payment(providerKey)
  
  // Extract order state from query result
  from orderQuery match
    QueryError |> nothing
    |>
      orderQuery
      first

let defaultOrderMap(contactId, order, delivery) =>

  let orderStatus = order.orderState match
    'pending' |> 'Pending'
    'cancelled' |> 'Cancelled'
    'confirmed' |> delivery.deliveryState match
      'completed' |> 'Shipped'
      'cancelled' |> 'Cancelled'
      |> 'Confirmed'
    'completed' |> 'Shipped'
  
  let paymentMethods =
    order.paymentCaptures
    select (c: {
      amount: number
      payment: {
        providerKey: text
      }
    }) => {
      description = c.payment.providerKey
      value = c.amount
    }

  let items =
    delivery.orderLines
    select (line: {
      productNumber: text
      description: text
      quantity: number
      orderLineTotal: number
      image: text
    }) => {
      sku = line.productNumber
      quantity = line.quantity
      grossPaidPrice = line.orderLineTotal
      description = line.description
      imageUrl = line.image
      // TODO targetUrl = 
    }

  from {
    contact = {
      matchKeyType = 'ContactId'
      matchKey = contactId
    }
    orderNumber = delivery.deliveryNumber
    orderStatus = orderStatus
    paymentStatus = 'Paid'
    createdDate = delivery.createdAt
    // TODO shippedDate = 
    storeId = 'TODO'
    currency = order.currencyCode
    paymentMethods = paymentMethods
    items = items
  }

let withTotalGrossPrice (voyadoOrder) |>
  voyadoOrder with {
    totalGrossPrice =
      voyadoOrder.items
      select (i: {
        grossPaidPrice: number
      }) => i.grossPaidPrice
      sum
  }

from {
  createOrUpdateOrder = (orderId: uuid) =>

    let order = getOrder(orderId)

    let mapOrder (contactId, order, delivery) =>
      defaultOrderMap(contactId, order, delivery)
      // Pass to custom mapper (if configured)
      // Custom mapper must access a record with any fields and a deliveryId
      // Custom mapper can extend the incoming value (using with), query graph for additional data etc.

    from order match
      nothing |> 'Order not found'
      when (order.deliveries count) == 0 |> 'No deliveries on order'
      |> findContact(order.invoiceAddress.email) match
        (contactId: text) |>
          order.deliveries
          select d =>
            web.http(
              $'{baseUrl}/api/v2/orders'
              {
                method = 'POST'
                headers = {
                  'User-Agent' -> 'Hantera'
                  'apikey' -> $'{apiKey}'
                  'Content-Type' -> 'application/json'
                }
                body = mapOrder(contactId, order, d) withTotalGrossPrice
              }
            ) match
              HttpResponseOk |> $'Exported delivery {d.deliveryNumber}'
              (res: HttpResponse) |> $'Failed to export delivery {d.deliveryNumber}: ({res.content})'
        |> 'Contact not found'

}
